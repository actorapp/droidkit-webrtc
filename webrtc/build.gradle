buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
    }
}
apply plugin: 'android-library'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

sourceCompatibility = 1.6
group = 'com.droidkit'
version = fullVersion

android {
    compileSdkVersion 19
    buildToolsVersion "20.0.0"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 19
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        abortOnError false
    }
}

// Android JavaDocs
android.libraryVariants.all { variant ->
    task("${variant.name}Javadoc", type: Javadoc) {
        destinationDir = new File("$project.buildDir/javadoc/$variant.name")

        source = variant.javaCompile.source
        exclude '**/BuildConfig.java'
        exclude '**/R.java'

        ext.androidJar = "${android.plugin.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
    }

    task("${variant.name}JavadocJar", type: Jar, dependsOn: "${variant.name}Javadoc") {
        classifier 'javadoc'
        destinationDir = new File("$project.buildDir/libs/")
        from "$project.buildDir/javadoc/$variant.name"
    }

    task("${variant.name}SourcesJar", type: Jar) {
        classifier 'sources'

        destinationDir = new File("$project.buildDir/libs/")

        from variant.javaCompile.source
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
    }
}

artifacts {
    archives releaseJavadocJar
    archives releaseSourcesJar
}


if (project.hasProperty("ossrhUsername") && project.hasProperty("ossrhPassword")) {

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'DroidKit-WebRTC'
                    packaging 'aar'
                    description 'DroidKit WebRTC is port of official WebRTC implementation for android'
                    url 'https://github.com/secretapphd/droidkit-webrtc'

                    scm {
                        url 'scm:git@github.com:secretapphd/droidkit-webrtc.git'
                        connection 'scm:git@github.com:secretapphd/droidkit-webrtc.git'
                        developerConnection 'scm:git@github.com:secretapphd/droidkit-webrtc.git'
                    }

                    licenses {
                        license {
                            name 'The MIT License (MIT)'
                            url 'http://opensource.org/licenses/MIT'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'ex3ndr'
                            name 'Stepan Korshakov'
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:19.+'
}